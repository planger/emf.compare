--- Q7 testcase ---
Format-Version: 1.0
Q7-vendor: www.xored.com/q7
Contexts: _SapwEL4pEeOsAoUpt_cnqw,_LOMGYGrvEeK_JdLra2Bk5A,_Bk3RQG76EeSzdu5rkqh7kg
Element-Name: ts009_tc008_logicalmodel_view_model_error
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _jbsTsG74EeSzdu5rkqh7kg
Q7-Runtime-Version: 1.3.14.201410230950
Save-Time: 11/18/14 9:19 AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

This test just checks that if the synchronization model fails, a popup opens and the logical model view is cleaned.

1. Select Window/Show View/Other...
2. Click on EMF Compare/Logical Model View item
3. Click on the corrupted model
// Assert that the view is not populated (because the synchronization is not active)
4. Activate the synchronization
// Assert that a popup opens.
// Assert that the error log contains entries
// Assert that the logical model view is empty
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

get-menu "Window/Show View/Error Log" | click
try {
	get-view "Error Log" | get-button "Delete Log" | click
	get-window "Confirm Delete" | get-button OK | click
} -catch {
	// Do nothing
}

//1. Select Window/Show View/Other...
get-menu "Window/Show View/Other..." | click
//2. Click on EMF Compare/Logical Model View item
with [get-window "Show View"] {
    get-tree | select "EMF Compare/Logical Model View"
    get-button OK | click
}

//3. Click on the corrputed model
// Assert that the view is not populated (because the synchronization is not active)
get-view "Package Explorer" | get-tree | select "Project3/left.uml"
get-view "Logical Model View" | get-tree | get-property itemCount | equals 0 | verify-true
//4. Activate the synchronization
get-view "Logical Model View" | get-button "Synchronize with Editor and Selection" | click
// Assert that a popup opens.
// Assert that the error log contains entries
// Assert that the logical model view is empty
get-window "EMF Compare - Logical Model View" | get-property title | equals "EMF Compare - Logical Model View" 
    | verify-true
get-window "EMF Compare - Logical Model View" | get-button OK | click

get-tab-folder -index 3 | get-property activePage | equals "Error Log" | verify-true
get-view "Error Log" | get-tree | get-item "Problems occurred while computing the synchronization model" 
    | get-property caption | equals "Problems occurred while computing the synchronization model" | verify-true
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
