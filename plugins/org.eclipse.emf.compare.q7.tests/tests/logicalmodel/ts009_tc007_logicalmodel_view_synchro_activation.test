--- Q7 testcase ---
Format-Version: 1.0
Q7-vendor: www.xored.com/q7
Contexts: _SapwEL4pEeOsAoUpt_cnqw,_LOMGYGrvEeK_JdLra2Bk5A,_KpbEwGNDEeSnVZVQFV_quQ
Element-Name: ts009_tc007_logicalmodel_view_synchro_activation
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _ffwCkW74EeSzdu5rkqh7kg
Q7-Runtime-Version: 1.3.14.201410230950
Save-Time: 11/18/14 9:05 AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

This test just checks the synchronization button activation populates the Logical Model View.

1. Select Window/Show View/Other...
2. Click on EMF Compare/Logical Model View item
3. Click on a model
// Assert that the view is not populated (because the synchronization is not active)
4. Activate the synchronization
// Assert that the view is populated with the logical models computing from the selected model(because the synchronization is active)
5. Deactivate the synchronization
6. Open a model
// Assert that the view is populated with the logical models computing from old selected model
7. Activate the synchronization
// Assert that the view is populated with the logical models computing from the opened model(because the synchronization is active)
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//1. Select Window/Show View/Other...
get-menu "Window/Show View/Other..." | click
//2. Click on EMF Compare/Logical Model View item
with [get-window "Show View"] {
    get-tree | select "EMF Compare/Logical Model View"
    get-button OK | click
}

//3. Click on a model
// Assert that the view is not populated (because the synchronization is not active)
get-view "Package Explorer" | get-tree | select "UML-Library/left/model.di"
get-view "Logical Model View" | get-tree | get-property itemCount | equals 0 | verify-true
//4. Activate the synchronization
get-view "Logical Model View" | get-button "Synchronize with Editor and Selection" | click
// Assert that the view is populated with the logical models computing from the selected model(because the synchronization is active)
with [get-view "Logical Model View" | get-tree] {
    get-property itemCount | equals 3 | verify-true
    get-property "getItems().TreeItem[0].getText()" | equals "model.di - /UML-Library/left" | verify-true
    get-property "getItems().TreeItem[1].getText()" | equals "model.notation - /UML-Library/left" | verify-true
    get-property "getItems().TreeItem[2].getText()" | equals "model.uml - /UML-Library/left" | verify-true
}
//5. Deactivate the synchronization
get-view "Logical Model View" | get-button "Synchronize with Editor and Selection" | click
//6. Open a model
get-view "Package Explorer" | get-tree | select "UML-Library/right/model.di" | double-click
// Assert that the view is populated with the logical models computing from old selected model
with [get-view "Logical Model View" | get-tree] {
    get-property itemCount | equals 3 | verify-true
    get-property "getItems().TreeItem[0].getText()" | equals "model.di - /UML-Library/left" | verify-true
    get-property "getItems().TreeItem[1].getText()" | equals "model.notation - /UML-Library/left" | verify-true
    get-property "getItems().TreeItem[2].getText()" | equals "model.uml - /UML-Library/left" | verify-true
}
//7. Activate the synchronization
get-view "Logical Model View" | get-button "Synchronize with Editor and Selection" | click
// Assert that the view is populated with the logical models computing from the opened model(because the synchronization is active)
with [get-view "Logical Model View" | get-tree] {
    get-property itemCount | equals 3 | verify-true
    get-property "getItems().TreeItem[0].getText()" | equals "model.di - /UML-Library/right" | verify-true
    get-property "getItems().TreeItem[1].getText()" | equals "model.notation - /UML-Library/right" | verify-true
    get-property "getItems().TreeItem[2].getText()" | equals "model.uml - /UML-Library/right" | verify-true
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
